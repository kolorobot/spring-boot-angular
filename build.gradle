buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url 'https://dl.bintray.com/ilyaai/maven'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('ro.isdc.wro4j.gradle:wro4j-gradle-plugin:1.7.9.Beta2')
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'wro4j'

jar {
    baseName = 'demo'
    version = '1.0.0'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    compile('org.springframework.boot:spring-boot-starter-web')

    /* Thymeleaf with LEGACY5 support */
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('net.sourceforge.nekohtml:nekohtml:1.9.22')

    /* Utils */
    compile('com.google.guava:guava:19.0')

    /* Webjars */
    compile('org.webjars.bower:angular:1.5.0')
    compile('org.webjars.bower:angular-ui-router:0.2.18')
    compile('org.webjars.bower:bootstrap:3.3.6') {
        exclude module: 'jquery'
    }
    compile('org.webjars.bower:jquery:2.2.0')
    /* Allows to write version agnostic paths in templates, like <script src="/jquery/jquery.min.js"/>. */
    /* See org.springframework.web.servlet.resource.WebJarsResourceResolver */
    compile('org.webjars:webjars-locator:0.30')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.assertj:assertj-core:3.3.0')
}

processResources {
    /* Apply filtering only to application.properties to not corrupt binary file (e.g. fonts, docx) during this phase */
    filesMatching('application.properties') {
        filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
                /* Run the build with -PactiveProfiles to set active profiles in application.properties */
                activeProfiles: project.getProperties().containsKey('activeProfiles') ? project.property('activeProfiles') : ''
        ]
    }
}

webResources {
    srcMainDir project.file('src/main/resources/static/')

    bundle ('app.min') {
        js 'app/js/module.js', 'app/js/configuration/*.js', 'app/js/controllers/*.js', 'app/js/services/*.js', 'app/js/directives/*.js'
        preProcessor 'jsMin'
    }
    bundle ('main.min') {
        css '/css/main.css'
        preProcessor 'cssUrlRewriting', 'cssImport', 'semicolonAppender'
        postProcessor  'cssVariables', 'cssMinJawr', 'jsMin'
    }
}

bootRun {
    // It is for running application with profiles
    // e.g. gradle bootRun -Dspring.profiles.active=embedded,basic
    systemProperties = System.properties
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}
